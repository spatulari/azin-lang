set(AZIN_KNOWN_CXX_STANDARDS 20 23 26)

list(GET AZIN_KNOWN_CXX_STANDARDS 0 AZIN_MIN_KNOWN)

list(LENGTH AZIN_KNOWN_CXX_STANDARDS _std_count)

if (_std_count GREATER 1)
    list(GET AZIN_KNOWN_CXX_STANDARDS -2 AZIN_DEFAULT_PREFERRED)
else ()
    set(AZIN_DEFAULT_PREFERRED ${AZIN_MIN_KNOWN})
endif ()

set(AZIN_PREFERRED_CXX_STANDARD ${AZIN_DEFAULT_PREFERRED} CACHE STRING "Preferred C++ standard")
set(AZIN_MINIMUM_CXX_STANDARD ${AZIN_MIN_KNOWN} CACHE STRING "Minimum supported C++ standard")

option(AZIN_OPPORTUNISTIC_UPGRADE "Use highest compiler-supported C++ standard" ON)

set_property(CACHE AZIN_PREFERRED_CXX_STANDARD PROPERTY STRINGS ${AZIN_KNOWN_CXX_STANDARDS})
set_property(CACHE AZIN_MINIMUM_CXX_STANDARD PROPERTY STRINGS ${AZIN_KNOWN_CXX_STANDARDS})

if (NOT AZIN_PREFERRED_CXX_STANDARD IN_LIST AZIN_KNOWN_CXX_STANDARDS)
    message(FATAL_ERROR "Invalid AZIN_PREFERRED_CXX_STANDARD: ${AZIN_PREFERRED_CXX_STANDARD}")
endif ()

if (NOT AZIN_MINIMUM_CXX_STANDARD IN_LIST AZIN_KNOWN_CXX_STANDARDS)
    message(FATAL_ERROR "Invalid AZIN_MINIMUM_CXX_STANDARD: ${AZIN_MINIMUM_CXX_STANDARD}")
endif ()

if (AZIN_MINIMUM_CXX_STANDARD GREATER AZIN_PREFERRED_CXX_STANDARD)
    message(FATAL_ERROR
            "Minimum C++${AZIN_MINIMUM_CXX_STANDARD} cannot exceed preferred C++${AZIN_PREFERRED_CXX_STANDARD}"
    )
endif ()

set(AZIN_MAX_CXX_STANDARD 0)

foreach (std IN LISTS AZIN_KNOWN_CXX_STANDARDS)
    if ("cxx_std_${std}" IN_LIST CMAKE_CXX_COMPILE_FEATURES)
        set(AZIN_MAX_CXX_STANDARD ${std})
    endif ()
endforeach ()

if (AZIN_MAX_CXX_STANDARD EQUAL 0)
    message(FATAL_ERROR "No supported C++ standard found")
endif ()

if (AZIN_OPPORTUNISTIC_UPGRADE)
    set(AZIN_CXX_STANDARD ${AZIN_MAX_CXX_STANDARD})
else ()
    set(AZIN_CXX_STANDARD ${AZIN_PREFERRED_CXX_STANDARD})

    if (AZIN_CXX_STANDARD GREATER AZIN_MAX_CXX_STANDARD)
        set(AZIN_CXX_STANDARD ${AZIN_MAX_CXX_STANDARD})
    endif ()
endif ()

if (AZIN_CXX_STANDARD LESS AZIN_MINIMUM_CXX_STANDARD)
    message(FATAL_ERROR
            "Compiler supports C++${AZIN_MAX_CXX_STANDARD}, "
            "but Azin requires at least C++${AZIN_MINIMUM_CXX_STANDARD}"
    )
endif ()

function(azin_apply_cxx_standard target)
    target_compile_features(${target} PUBLIC cxx_std_${AZIN_CXX_STANDARD})
    set_target_properties(${target} PROPERTIES
            CXX_STANDARD_REQUIRED ON
            CXX_EXTENSIONS OFF
    )
endfunction()

message(STATUS "")
message(STATUS "C++ configuration")
message(STATUS "  Compiler max : C++${AZIN_MAX_CXX_STANDARD}")
message(STATUS "  Preferred    : C++${AZIN_PREFERRED_CXX_STANDARD}")
message(STATUS "  Minimum      : C++${AZIN_MINIMUM_CXX_STANDARD}")
message(STATUS "  Opportunistic: ${AZIN_OPPORTUNISTIC_UPGRADE}")
message(STATUS "  Selected     : C++${AZIN_CXX_STANDARD}")
message(STATUS "")